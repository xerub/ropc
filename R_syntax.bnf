translation_unit	: stat_or_decl_list
			;
stat_or_decl_list	: stat_or_decl
			| stat_or_decl_list stat_or_decl
			;
stat_or_decl		: stat
			| external_decl ';'
			;
external_decl		: "extern" ID
			| "extern" ID attribute_spec
			| "extern" ID '=' NUMBER
			| "extern" ID attribute_spec '=' NUMBER
			;
stat			: labeled_stat
			| ';'
			| '{' stat_or_decl_list '}'
			| jump_stat
			| selection_stat
			| assignment_exp ';'
			;
labeled_stat		: ID ':' stat
			;
jump_stat		: "goto" ID ';'
			| "break" ';'
			| "continue" ';'
			;
selection_stat		: "if" conditional_exp stat
			| "if" conditional_exp stat "else" stat
			| "do" stat "while" conditional_exp ';'
			| "while" conditional_exp stat
			;
conditional_exp		: '(' assignment_exp ')'
			|  '!' '(' assignment_exp ')'
			;
assignment_exp		: or_exp
			| lvalue_exp '=' assignment_exp
			;
or_exp			: xor_exp
			| or_exp '&' xor_exp
			;
xor_exp			: and_exp
			| xor_exp '&' and_exp
			;
and_exp			: additive_exp
			| and_exp '&' additive_exp
			;
additive_exp		: multiplicative_exp
			| additive_exp '+' multiplicative_exp
			| additive_exp '-' multiplicative_exp
			;
multiplicative_exp	: rvalue_exp
			| multiplicative_exp '*' rvalue_exp
			| multiplicative_exp '/' rvalue_exp
			;
rvalue_exp		: immediate_exp
			| lvalue_exp
			| ID '(' ')'
			| ID '(' argument_exp_list ')'
			| attribute_spec ID '(' ')'
			| attribute_spec ID '(' argument_exp_list ')'
			| '(' or_exp ')'
			;
argument_exp_list	: or_exp
			| or_exp ',' argument_exp_list
			;
lvalue_exp 		: '*' ID
			| ID
			| type_qualifier ID
			;
type_qualifier		: "const" | "volatile"
			;
immediate_exp		: NUMBER
			| '+' NUMBER
			| '-' NUMBER
			| STRING
			| '@' STRING
			| '&' ID
			| '{' initializer_list '}'
			;
initializer_list	: immediate_exp
			| initializer_list ',' immediate_exp
			;
attribute_spec		: '[' '[' attribute_list ']' ']'
			;
attribute_list		: attribute
			| attribute ',' attribute_list
			;
attribute		: "noreturn" | "stdcall" | "stack"
			| "regparm" = NUMBER
			| "stack" = NUMBER
			;
